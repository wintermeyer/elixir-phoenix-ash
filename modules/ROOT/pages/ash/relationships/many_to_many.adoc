### many_to_many

`many_to_many` is a special case of `has_many` where the relationship is
symmetric. That means that the relationship is defined on both sides of the
relationship. A good example is a `Tag` resource that can be assigned to
multiple (or none) `Product` resources and a `Product` resource has multiple
(or none) `Tag` resources assigned to it.

[source]
----
+---------+     +------------+     +--------+
| Product |     | ProductTag |     | Tag    |
+---------+     +------------+     +--------+
| id      |<--->| product_id |     | name   |
| name    |     | tag_id     |<--->| id     |
| price   |     |            |     |        |
+---------+     +------------+     +--------+
----

To keep our source code footprint minimal we use the
xref:ash/relationships/index.adoc#ash-relationships-setup[relationship setup]
 example code which has just the `Product` resource and add a `Tag` and a
 `ProductTag` resource that is the join table between `Tag` and `Product`.

[source,elixir,title='lib/app/shop/resources/tag.ex']
----
defmodule App.Shop.Tag do
  use Ash.Resource, data_layer: Ash.DataLayer.Ets

  attributes do
    uuid_primary_key :id
    attribute :name, :string
  end

  relationships do
    many_to_many :products, App.Shop.Product do
      through App.Shop.ProductTag
      source_attribute_on_join_resource :tag_id
      destination_attribute_on_join_resource :product_id
    end <1>
  end

  actions do
    defaults [:read, :update, :destroy]

    create :create do
      primary? true
      argument :products, {:array, :map}

      change manage_relationship(:products,
               type: :append_and_remove,
               on_no_match: :create
             ) <2>
    end
  end

  code_interface do
    define_for App.Shop
    define :create
    define :read
    define :by_id, get_by: [:id], action: :read
    define :by_name, get_by: [:name], action: :read
    define :update
    define :destroy
  end
end
----

<1> The configuration for the `many_to_many` relationship to `product`.
<2> This allows us to create a `Tag` resource with a list of `Product` resources
    assigned to it.

[source,elixir,title='lib/app/shop/resources/product_tag.ex']
----
defmodule App.Shop.ProductTag do
  use Ash.Resource, data_layer: Ash.DataLayer.Ets

  actions do
    defaults [:create, :read, :destroy] <1>
  end

  relationships do <2>
    belongs_to :product, App.Shop.Product do
      primary_key? true
      allow_nil? false
    end

    belongs_to :tag, App.Shop.Tag do
      primary_key? true
      allow_nil? false
    end
  end
end
----

<1> No need for an `:update` action. Entries in the join table are immutable. You can delete but not change them.
<2> The clue between the `Product` and the `Tag` resources.

[source,elixir,title='lib/app/shop/resources/product.ex']
----
defmodule App.Shop.Product do
  use Ash.Resource, data_layer: Ash.DataLayer.Ets

  attributes do
    uuid_primary_key :id
    attribute :name, :string
    attribute :price, :decimal
  end

  relationships do
    many_to_many :tags, App.Shop.Tag do <1>
      through App.Shop.ProductTag
      source_attribute_on_join_resource :product_id
      destination_attribute_on_join_resource :tag_id
    end
  end

  actions do
    defaults [:read, :update, :destroy]

    create :create do
      primary? true
      argument :tags, {:array, :map}

      change manage_relationship(:tags,
               type: :append_and_remove,
               on_no_match: :create
             ) <2>
    end
  end

  code_interface do
    define_for App.Shop
    define :create
    define :read
    define :by_id, get_by: [:id], action: :read
    define :by_name, get_by: [:name], action: :read
    define :update
    define :destroy
  end
end
----

<1> The configuration for the `many_to_many` relationship to `tag`.
<2> This allows us to create a `Product` resource with a list of `Tag` resources
    assigned to it.

#### Example Usage in iex

Let's use the following data for our example.

NOTE: Ash does use UUIDs. I use integer IDs in the example
table because it's easier to read for humans.

[source]
----
Product:
+----+--------+
| id | name   |
+----+--------+
| 1  | Apple  |
| 2  | Banana |
| 3  | Cherry |
+----+--------+

Tag:
+----+---------+
| id | Name    |
+----+---------+
| 1  | Sweet   |
| 2  | Tropical|
| 3  | Red     |
+----+---------+

ProductTag:
+-----------+-------+
| product_id| tag_id|
+-----------+-------+
| 1         | 1     |  (Apple is Sweet)
| 1         | 3     |  (Apple is Red)
| 2         | 1     |  (Banana is Sweet)
| 2         | 2     |  (Banana is Tropical)
| 3         | 3     |  (Cherry is Red) <1>
+-----------+-------+
----

<1> Not a complete list of all real world combinations.
I am aware that cherries can be sweet too. ðŸ˜‰

Open the iex and create all the products with their tags.

```elixir
$ iex -S mix
iex(1)> alias App.Shop.Tag
iex(2)> alias App.Shop.Product
iex(3)> sweet = Tag.create!(%{name: "Sweet"})
iex(4)> tropical = Tag.create!(%{name: "Tropical"})
iex(5)> red = Tag.create!(%{name: "Red"})
iex(6)> Product.create!(%{name: "Apple", tags: [sweet, red]})
iex(7)> Product.create!(%{name: "Banana", tags: [sweet, tropical]})
iex(8)> Product.create!(%{name: "Cherry", tags: [red]})
```

Now we can read all products with their tags and vice versa.

```elixir
iex(9)> Product.read!(load: [:tags]) |>
...(9)> Enum.map(fn product ->
...(9)>   %{
...(9)>     product_name: product.name,
...(9)>     tag_names: Enum.map(product.tags, & &1.name)
...(9)>   }
...(9)> end)
[
  %{product_name: "Banana", tag_names: ["Sweet", "Tropical"]},
  %{product_name: "Apple", tag_names: ["Sweet", "Red"]},
  %{product_name: "Cherry", tag_names: ["Red"]}
]

iex(10)> Tag.read!(load: [:products]) |>
...(10)> Enum.map(fn tag ->
...(10)>   %{
...(10)>     tag_name: tag.name,
...(10)>     product_names: Enum.map(tag.products, & &1.name)
...(10)>   }
...(10)> end)
[
  %{tag_name: "Tropical", product_names: ["Banana"]},
  %{tag_name: "Red", product_names: ["Cherry", "Apple"]},
  %{tag_name: "Sweet", product_names: ["Apple", "Banana"]}
]
```

[[ash-many_to_many-sideloading]]
#### many_to_many sideloading by default

Be default Ash will not load the join table entries. You can change this with the
`:load` option in the `:read` action:

[source,elixir,title='lib/app/product.ex']
----
  # [...]

  actions do
    defaults [:update, :destroy] <1>

    read :read do
      primary? true
      prepare build(load: [:tags]) <2>
    end

    create :create do
      primary? true
      argument :tags, {:array, :map}

      change manage_relationship(:tags,
               type: :append_and_remove,
               on_no_match: :create
             )
    end
  end

  # [...]
----

<1> Don't forget to remove `:read` here.
<2> Always sideload the `tags` relationship.

[source,elixir,title='lib/app/product.ex']
----
  # [...]

  actions do
    defaults [:update, :destroy] <1>

    read :read do
      primary? true
      prepare build(load: [:products]) <2>
    end

    create :create do
      primary? true
      argument :products, {:array, :map}

      change manage_relationship(:products,
               type: :append_and_remove,
               on_no_match: :create
             )
    end
  end

  # [...]
----

<1> Don't forget to remove `:read` here.
<2> Always sideload the `products` relationship.

Let's use it in the `iex` console:

```elixir
$ iex -S mix
iex(1)> alias App.Shop.Tag
iex(2)> alias App.Shop.Product
iex(3)> sweet = Tag.create!(%{name: "Sweet"})
iex(4)> tropical = Tag.create!(%{name: "Tropical"})
iex(5)> red = Tag.create!(%{name: "Red"})
iex(6)> Product.create!(%{name: "Apple", tags: [sweet, red]})
iex(7)> Product.create!(%{name: "Banana", tags: [sweet, tropical]})
iex(8)> Product.create!(%{name: "Cherry", tags: [red]})

iex(9)> Product.read! |> <1>
...(9)> Enum.map(fn product ->
...(9)>   %{
...(9)>     product_name: product.name,
...(9)>     tag_names: Enum.map(product.tags, & &1.name)
...(9)>   }
...(9)> end)
[
  %{product_name: "Banana", tag_names: ["Sweet", "Tropical"]},
  %{product_name: "Apple", tag_names: ["Sweet", "Red"]},
  %{product_name: "Cherry", tag_names: ["Red"]}
]

iex(10)> Tag.read! |> <2>
...(10)> Enum.map(fn tag ->
...(10)>   %{
...(10)>     tag_name: tag.name,
...(10)>     product_names: Enum.map(tag.products, & &1.name)
...(10)>   }
...(10)> end)
[
  %{tag_name: "Tropical", product_names: ["Banana"]},
  %{tag_name: "Red", product_names: ["Cherry", "Apple"]},
  %{tag_name: "Sweet", product_names: ["Apple", "Banana"]}
]
```

<1> We don't have to specify `load: [:tags]` here because we set it as the default in the `:read` action.
<2> We don't have to specify `load: [:tags]` here because we set it as the default in the `:read` action.

[[ash-many_to_many-update]]
#### Update many_to_many relationships

Sometimes we want to update the `tags` of a `product` resource.
It feels most natural to do it via the `update` action of the `product` resource.
For that to work we have to define a custom `:update` action that will update the `tags`
relationship. We can more or less copy the code from the `:create` action for that:

[source,elixir,title='lib/app/shop/resources/product.ex']
----
  # [...]
  actions do
    defaults [:read, :destroy]

    create :create do
      primary? true
      argument :tags, {:array, :map}

      change manage_relationship(:tags,
               type: :append_and_remove,
               on_no_match: :create
             )
    end

    update :update do <1>
      primary? true
      argument :tags, {:array, :map}

      change manage_relationship(:tags,
               type: :append_and_remove,
               on_no_match: :create
             )
    end
  end
  # [...]
----

<1> Same as the `:create` action just with `:update`.

Let's use it in the `iex` console. We first create a product
with two tags and than we update it to have only one tag:

```elixir
$ iex -S mix
iex(1)> alias App.Shop.Tag
iex(2)> alias App.Shop.Product
iex(3)> good_deal = Tag.create!(%{name: "Good deal"})
iex(4)> yellow = Tag.create!(%{name: "Yellow"})
iex(5)> Product.create!(%{name: "Banana", tags: [yellow, good_deal]}) <1>
iex(6)> Product.by_name!("Banana", load: [:tags]).tags |> Enum.map(& &1.name) <2>
["Yellow", "Good deal"]
iex(7)> banana = Product.by_name!("Banana") <3>
iex(8)> Product.update!(banana, %{tags: [yellow]}) <4>
iex(9)> Product.by_name!("Banana", load: [:tags]).tags |> Enum.map(& &1.name) <5>
["Yellow"]
```

<1> Create a new product with two tags.
<2> Query the just created product and print the two tag names.
<3> Store the product in the variable `banana` for later use.
<4> Update the product to have only one tag.
<5> Double check that the product really only has one tag.

The between resource `ProductTag` is automatically updated.
And by update I mean that one entry was deleted.

[[ash-many_to_many-uniqueness]]
#### Unique Tags

We don't want to have multiple tags with the same name. But right
now this is possible:

```elixir
$ iex -S mix
iex(1)> alias App.Shop.Tag
iex(2)> Tag.create!(%{name: "Yellow"}).id
"d206b758-253d-4f06-9773-5423ae1f6027"
iex(3)> Tag.create!(%{name: "Yellow"}).id
"5d66386c-bb02-4a8e-bf2a-5457477a6da2"
iex(4)> Tag.create!(%{name: "Yellow"}).id
"3497214e-83a0-43bd-b087-143af5ef8c37"
iex(5)> Tag.read! |> Enum.map(& &1.name)
["Yellow", "Yellow", "Yellow"]
```

We can fix this with `identities` in the resource:

[source,elixir,title='lib/app/shop/resources/tag.ex']
----
defmodule App.Shop.Tag do
  use Ash.Resource, data_layer: Ash.DataLayer.Ets

  attributes do
    uuid_primary_key :id
    attribute :name, :string
  end

  identities do
    # identity :unique_name, [:name] <1>

    identity :name, [:name] do <2>
      pre_check_with App.Shop <3>
    end
  end
  # [...]
----

<1> Use this if you use a PostgreSQL database. Don't forget to run a `mix ash.codegen` after you added it.
<2> Use this if your use a ETS data layer like we do in this example.
<3> Since ETS doesn't support unique indexes we have to check for uniqueness before we create it.

Now we can not create multiple tags with the same name anymore:

```elixir
$ iex -S mix
iex(1)> alias App.Shop.Tag
iex(2)> Tag.create!(%{name: "Yellow"}).id
"f03e163f-5a17-4ea4-b708-f2089234d642"
iex(3)> Tag.create!(%{name: "Yellow"}).id
** (Ash.Error.Invalid) Input Invalid

* name: has already been taken
    (ash 2.14.18) lib/ash/api/api.ex:2179: Ash.Api.unwrap_or_raise!/3
iex(3)> Tag.create(%{name: "Yellow"}).id
** (KeyError) key :id not found in: {:error,
 %Ash.Error.Invalid{
   errors: [
     %Ash.Error.Changes.InvalidChanges{
       fields: [:name],
       message: "has already been taken",
       [...]
```
